
#Использовать delegate

#Использовать logos

Перем Спек экспорт;
Перем ПодробноеОписание Экспорт;

Перем ДействиеВыполнения Экспорт;
Перем ДействиеПередВыполнением Экспорт;
Перем ДействиеПослеВыполнения Экспорт;

Перем Имя; // текст
Перем Синонимы; // массив строк
Перем Описание; // текст
Перем ВложенныеКоманды;  // Массив классов КомандаПриложения
Перем Опции; // Структура Ключ - Значение (Структура описание опции)
Перем Аргументы; // Структура  

Перем ОпцииИндекс; // Соответствие
Перем АргументыИндекс; // Соответствие

Перем КлассРеализации;

Перем КомандыРодители Экспорт;
Перем ТипОпцииКоманды; // временная структура

Перем fsm;
Перем НачальноеСостояние;

Перем Лог;

Функция ДобавитьПодкоманду(ИмяПодкоманды, ОписаниеПодкоманды, КлассРеализацииПодкоманды) Экспорт

	Подкоманда = Новый КомандаПриложения(ИмяПодкоманды, ОписаниеПодкоманды, КлассРеализацииПодкоманды);
	ВложенныеКоманды.Добавить(Подкоманда);
	Возврат Подкоманда;
	
КонецФункции

Процедура ПриСозданииОбъекта(ИмяКоманды, ОписаниеКоманды, КлассРеализацииКоманды)
	
	Синонимы = СтрРазделить(ИмяКоманды, " ", Ложь);
	Имя = Синонимы[0];
	Описание = ОписаниеКоманды;
	КлассРеализации = КлассРеализацииКоманды;

	ВложенныеКоманды = Новый Массив;
	КомандыРодители = Новый Массив;
	Опции = Новый Соответствие;
	Аргументы = Новый Соответствие;
	
	ОпцииИндекс = Новый Соответствие;
	АргументыИндекс = Новый Соответствие;
	
	Спек = "";
	ПодробноеОписание = "";

	ДействиеВыполнения = Делегаты.Создать(КлассРеализацииКоманды, "ВыполнитьКоманду");
	ДействиеПередВыполнением = Делегаты.Создать(КлассРеализацииКоманды, "ПередВыполнением");
	ДействиеПослеВыполнения = Делегаты.Создать(КлассРеализацииКоманды, "ПослеВыполнения");
	

	ТипОпцииКоманды = Новый Структура;
	ТипОпцииКоманды.Вставить("Опция", "Опция");
	ТипОпцииКоманды.Вставить("Аргумент", "Аргумент");

	fsm = Новый ВыборСовпадений();

КонецПроцедуры

Функция ПолучитьИмяКоманды() Экспорт
	Возврат Имя;	
КонецФункции

Процедура ЧтениеАргументов(АргументыCLI) Экспорт

	nargsLen = ПолучитьОпцииИАргументы(АргументыCLI);

	Лог.Отладка("Количество входящих аргументов команды: %1", АргументыCLI.Количество());
	Лог.Отладка("Количество аргументов команды: %1", nargsLen);

	НачальныйИндексКоманды = 0;
	КонечныйИндексКоманды = nargsLen-1;
	
	Лог.Отладка("КонечныйИндексКоманды аргументов команды: %1", КонечныйИндексКоманды);
	
	
	МассивАргументовКПарсингу = Новый Массив;

	Для ИИ = 0 По КонечныйИндексКоманды Цикл
		МассивАргументовКПарсингу.Добавить(АргументыCLI[ИИ]);
	КонецЦикла;

	Лог.Отладка("Читаю аргументы строки");
	НачальноеСостояние.Прочитать(МассивАргументовКПарсингу);

	Если НЕ КонечныйИндексКоманды = НачальныйИндексКоманды
		И КонечныйИндексКоманды = АргументыCLI.ВГраница() Тогда
		
		Лог.Отладка("Выполняю полезную работу %1", Имя);
		ДействиеВыполнения.Исполнить();
		Возврат;
	КонецЕсли;

	ПервыйАргумент = АргументыCLI[КонечныйИндексКоманды+1];
		
	Для каждого ВложеннаяКоманда Из ВложенныеКоманды Цикл
		
		Если ВложеннаяКоманда.ЭтоСинонимКоманды(ПервыйАргумент) Тогда
		
			АргументыПодкоманды = Новый Массив;
			
			НачальныйИндекс = КонечныйИндексКоманды+2;
			
			Если НачальныйИндекс <= АргументыCLI.ВГраница() Тогда
				
				Для ИИ = НачальныйИндекс По АргументыCLI.ВГраница() Цикл
					АргументыПодкоманды.Добавить(АргументыCLI[ИИ]);
				КонецЦикла;
			
			КонецЕсли;

			ВложеннаяКоманда.НачалоЗапуска();
			ВложеннаяКоманда.ЧтениеАргументов(АргументыПодкоманды);
			Возврат;

		КонецЕсли;

	КонецЦикла;


	Если СтрНачинаетсяС(ПервыйАргумент,"-") Тогда
		 
		ВызватьИсключение "Не известна опция";

	КонецЕсли;

	ВызватьИсключение "Вызвать исключение не корректное использование";
	
КонецПроцедуры

Функция ПолучитьОпцииИАргументы(АргументыCLI)
	
	Количество = 0;
	Лог.Отладка("Приверяю аргументы. Количество %1", АргументыCLI.Количество());

	Для каждого ТекущийАргумент Из АргументыCLI Цикл
			
		Для каждого ВложеннаяКоманда Из ВложенныеКоманды Цикл
			
			Лог.Отладка("Ищю подчиненную команду %1", ВложеннаяКоманда.ПолучитьИмяКоманды());
			Если ВложеннаяКоманда.ЭтоСинонимКоманды(ТекущийАргумент) Тогда
				Лог.Отладка("Подчиненная команда %1 найдена", ВложеннаяКоманда.ПолучитьИмяКоманды());
				Возврат Количество;
			КонецЕсли;

		КонецЦикла;

		Количество = Количество +1;
		
	КонецЦикла;

	Возврат Количество;

КонецФункции

Функция ЭтоСинонимКоманды(СтрокаПроверки) Экспорт
	Возврат Не Синонимы.Найти(СтрокаПроверки) = Неопределено;
КонецФункции

Процедура НачалоЗапуска() Экспорт

	КомандыРодителиДляПодчиненной = Новый Массив;
	КомандыРодителиДляПодчиненной.Добавить(Имя);

	Для каждого КомандаРодитель Из КомандыРодители Цикл
		КомандыРодителиДляПодчиненной.Добавить(КомандаРодитель);
	КонецЦикла;

	Для каждого Подчиненнаякоманда Из ВложенныеКоманды Цикл
		Подчиненнаякоманда.КомандыРодители = КомандыРодителиДляПодчиненной;
	КонецЦикла;

	ДобавитьОпцииВИндекс();
	ДобавитьАргументыВИндекс();

	Если ПустаяСтрока(Спек) Тогда
		
		Если Опции.Количество() > 0 Тогда
			Спек = "[OPTIONS] ";
		КонецЕсли;

		Для каждого арг Из Аргументы Цикл
			Спек = Спек + арг.Ключ.Имя + " ";
		КонецЦикла;
		
	КонецЕсли;

	токены = СпекЛексер.ПолучитьСпекТокены(Спек);

	ПараметрыПарсера =  Новый Структура;
	ПараметрыПарсера.Вставить("Спек", Спек);	
	ПараметрыПарсера.Вставить("Опции", Опции);	
	ПараметрыПарсера.Вставить("Аргументы", Аргументы);	
	ПараметрыПарсера.Вставить("ОпцииИндекс", ОпцииИндекс);	
	ПараметрыПарсера.Вставить("АргументыИндекс", АргументыИндекс);	
	
	парсер = Новый СпекПарсер(токены, ПараметрыПарсера);
	НачальноеСостояние = парсер.Прочитать();

КонецПроцедуры

Функция Опция(Имя, Значение = "", Описание = "") Экспорт
	
	НоваяОпция = Новый ОпцияКоманды(Имя, Значение, Описание);
	Опции.Вставить(НоваяОпция, НоваяОпция);
	
	
	Возврат НоваяОпция;

КонецФункции

Функция Аргумент(Имя, Значение = "", Описание = "") Экспорт
	
	НовыйАргумент = Новый АргументКоманды(Имя, Значение, Описание);
	Аргументы.Вставить(НовыйАргумент, НовыйАргумент);
	
	Возврат НовыйАргумент;

КонецФункции

Процедура ДобавитьОпцию(КлассОпции) Экспорт

	Опции.Вставить(КлассОпции, КлассОпции);
	
КонецПроцедуры

Процедура ДобавитьАргумент(КлассАргумент) Экспорт
	
	Аргументы.Вставить(КлассАргумент, КлассАргумент);
		
КонецПроцедуры
	

Процедура ДобавитьОпцииВИндекс()
	
	Для каждого КлючЗначение Из Опции Цикл

		КлассОпции = КлючЗначение.Ключ;
		Если Не ПустаяСтрока(КлассОпции.ПеременнаяОкружения) Тогда
			
			КлассОпции.УстановитьЗначениеИзПеременнойОкружения();
	
		КонецЕсли;

		Для каждого ИмяПараметра Из КлассОпции.НаименованияПараметров Цикл
			
				ОпцииИндекс.Вставить(ИмяПараметра, КлассОпции);
		
		КонецЦикла;
	
	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьАргументыВИндекс()
	
	Для каждого КлючЗначение Из Аргументы Цикл
		
		КлассАргумента = КлючЗначение.Ключ;
		Если Не ПустаяСтрока(КлассАргумента.ПеременнаяОкружения) Тогда
			
			КлассАргумента.УстановитьЗначениеИзПеременнойОкружения();
	
		КонецЕсли;

		Для каждого ИмяПараметра Из КлассАргумента.НаименованияПараметров Цикл
			
				АргументыИндекс.Вставить(ИмяПараметра, КлассАргумента);
		
		КонецЦикла;
	
	КонецЦикла;

КонецПроцедуры

Лог = Логирование.ПолучитьЛог("oscript.lib.v8runner");
Лог.УстановитьУровень(УровниЛога.Отладка);