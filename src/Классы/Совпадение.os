
Перем Завершено Экспорт;
Перем МассивСоединений Экспорт;

Процедура ПриСозданииОбъекта()
	
	МассивСоединений = Новый Массив;
	Завершено = Ложь;

КонецПроцедуры

Функция Т(Парсер, СледующееСостояние) Экспорт
	
	МассивСоединений.Добавить(НовоеСоединениеСовпадений(Парсер, СледующееСостояние));
	Возврат СледующееСостояние;

КонецФункции

Функция НовоеСоединениеСовпадений(Парсер, СледующееСостояние)

	Возврат Новый Структура("Парсер, СледующееСостояние", Парсер, СледующееСостояние);
	
КонецФункции

Процедура Подготовить() Экспорт
	
	ПосетилиСостояниеСортировки = Новый Соответствие;
	ПосетилиСостояниеУпрощения = Новый Соответствие;
	fsm = новый ВыборСовпадений();
	fsm.СортировкаСоединений(ЭтотОбъект, ПосетилиСостояниеСортировки);
	fsm.УпроститьСоединения(ЭтотОбъект, ЭтотОбъект, ПосетилиСостояниеУпрощения);

КонецПроцедуры

Процедура Прочитать(ВходящиеАргументы) Экспорт
	
	Контекст = Новый КонтекстПарсеров;
	
	ПрименитьКонтекст(ВходящиеАргументы, Контекст);

	Лог.Отладка("Проверка контекста: 
	| количество опций: %1", Контекст.Опции.Количество());
	
КонецПроцедуры

Функция ПрименитьКонтекст(Знач ВходящиеАргументы, Контекст)
	
	Лог.Отладка("Применяю контекст: 
	| количество соединений %1
	| количество входящих аргументов %2", МассивСоединений.Количество(), ВходящиеАргументы.Количество());
	
	Если Завершено 
		И ВходящиеАргументы.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;

	Если ВходящиеАргументы.Количество() > 0 Тогда
		
		ПервыйАргумент = ВходящиеАргументы[0];
		Если Не Контекст.СбросОпций
			И СокрЛП(ПервыйАргумент) = "--" Тогда
			Контекст.СбросОпций = Истина;
			ВходящиеАргументы.Удалить(0);
		КонецЕсли;

	КонецЕсли;

	МассивСовпадений = Новый Массив;

	Для каждого Соединение Из МассивСоединений Цикл
		ЧистыйКонтекст = Новый КонтекстПарсеров;
		ЧистыйКонтекст.СбросОпций = Контекст.СбросОпций;

		РезультатПоиска = Соединение.Парсер.Поиск(ВходящиеАргументы, ЧистыйКонтекст);
		Если РезультатПоиска.РезультатПоиска Тогда
			МассивСовпадений.Добавить(НовоеСовпадение(Соединение, РезультатПоиска.Аргументы, ЧистыйКонтекст));
		КонецЕсли;

	КонецЦикла;
	
	Для каждого ЭлементСовпадения Из МассивСовпадений Цикл

		Если ЭлементСовпадения.Соединение.СледующееСостояние.ПрименитьКонтекст(ЭлементСовпадения.Результат, ЭлементСовпадения.Контекст)  Тогда
			Контекст.ПрисоединитьКонтекст(ЭлементСовпадения.Контекст);
			Возврат Истина;
		КонецЕсли;

	КонецЦикла;

	Возврат Ложь

КонецФункции
	
Функция НовоеСовпадение(Соединение, Результат, Контекст)
	Возврат Новый Структура("Соединение, Результат, Контекст", Соединение, Результат, Контекст)
КонецФункции