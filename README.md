# 1cli

[![Build Status](https://travis-ci.org/khorevaa/1cli.svg?branch=master)](https://travis-ci.org/khorevaa/1cli)
[![Coverage Status](https://coveralls.io/repos/github/khorevaa/1cli/badge.svg?branch=master)](https://coveralls.io/github/khorevaa/1cli?branch=master)

Данная библиотека для языка Oscript, позволяет создавать консольные приложения с разбором и проверкой аргументов. 

## Быстрый старт

### Пример простого приложения

```bsl
#Использовать 1cli

Перем Лог;

///////////////////////////////////////////////////////////////////////////////

Процедура ВыполнитьПриложение()

    Приложение = Новый КонсольноеПриложение(ПараметрыПриложения.ИмяПриложения(), "Помощник генерации приложения на основании шаблона 1cli");
    Приложение.Версия("v version", ПараметрыПриложения.Версия());

    Приложение.УстановитьОсновноеДействие(ЭтотОбъект)
    Приложение.Запустить(АргументыКоманднойСтроки);

КонецПроцедуры // ВыполнениеКоманды()

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

    Сообщить("Полезная работа");

КонецПроцедуры

///////////////////////////////////////////////////////


Попытка
    
    ВыполнитьПриложение();

Исключение

    Сообщить(ОписаниеОшибки());

КонецПопытки;
```

### Пример приложения с несколькими командами:

```bsl
#Использовать 1cli

///////////////////////////////////////////////////////////////////////////////

Процедура ВыполнитьПриложение()

    Приложение = Новый КонсольноеПриложение("1cli", "Помощник генерации приложения на основании шаблона 1cli");
    Приложение.Версия("v version","1.0.0");

    Приложение.ДобавитьПодкоманду("i init", "Инициализация структуры нового приложения", Новый КомандаInit);
    Приложение.ДобавитьПодкоманду("g generate", "Генерация элементов структуры приложения", Новый КомандаGenerate);

    Приложение.Запустить(АргументыКоманднойСтроки);

КонецПроцедуры // ВыполнениеКоманды()

///////////////////////////////////////////////////////

Попытка

    ВыполнитьПриложение();

Исключение

    Сообщить(ОписаниеОшибки());

КонецПопытки;
```

## Мотивация

|                                                                              | 1cli    | cmdline         |
|------------------------------------------------------------------------------|---------|-----------------|
| Встроенная команда help                                                      | ✓       | ✓               |
| Встроенная команда version                                                   | ✓       | ✓               |
| Команды                                                                      | ✓       | ✓               |
| Подкоманды                                                                   | ✓       |                 |
| Совмещение булевных (флаговых) опций  `-xyz`                                 | ✓       |                 |
| Совмещение опции и значения  `-fValue`                                       | ✓       |                 |
| Взаимоисключающие опции: `--start ❘ --stop`                                  | ✓       |                 |
| Необязательные опции : `[-a -b]` or `[-a [-b]]`                              | ✓       |                 |
| Проверка аргументов : `FILE PATH`                                            | ✓       |                 |
| Необязательные аргументы : `SRC [DST]`                                       | ✓       |                 |
| Повторение аргументов : `SRC... DST`                                         | ✓       |                 |
| Зависимость опций и аргументов друг от друга : `SRC [-f DST]`                | ✓       |                 |
| Формирование своей строки выполнения: `[-d ❘ --rm] FILE [КОМАНДА [АРГУМЕНТЫ...]]`  | ✓       |                 |

## Установка

Для установки необходимо: 
1. Скачать файл 1cli.ospx из раздела [releases](https://github.com/khorevaa/1cli/releases)
2. воспользоваться команды:

```
$ opm install -f <ПутьКФайлу>
```

## Базовые принципы

При создании приложения необходимо указать имя приложения и описание:

```bsl
Приложение = Новый КонсольноеПриложение("1cli", "Помощник генерации приложения на основании шаблона 1cli");
```

Определение функции выполнения команды:
Класс или модуль должен реализовывать экспортную процедуру:
```bsl
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
КонецПроцедуры
```
Передать данный класс через процедуру:
```bsl
Приложение.УстановитьОсновноеДействие(ЭтотОбъект)
```


Для добавления отображения версии через опции: ```-v, --version``` надо добавить строчку:

```bsl
Приложение.Версия("v version", "1.2.3");
```

Для запуска приложения необходимо добавить строчку: 

```bsl
Приложение.Запустить(АргументыКоманднойСтроки);
```

## Параметры команд/приложения

Все параметры разделяются на два типа
* Опция 
* Аргумент

## Опция

Опция может быть следующих простых типов:
* Булево
* Строка
* Число
* Дата
* Длительность

Так и принимать массивы данных типов, на пример:

* МассивЧисел
* МассивСтрок
* МассивДат
* МассивДлительностей

Пример `булево` опции:

```bsl
Отладка = Команда.Опция("v debug", ложь ,"Описание опции")
    .Флаговый() / тип опции булево
    .ВОкружении("ИМЯ_ПЕРЕМЕННОЙ")
    .ПоУмолчанию(Ложь)
    .СкрытьВСправке(); // Любой тип

```

`ВОкружении` Возможна передача нескольких переменных окружения разделенных через **пробел**

Подробное описание возможностей параметров команд и приложения [](./docs/ПараметрКоманды.md)

## Пример синтаксиса опций

### Для типа булево:

* `-f` : одно тире для коротких имен
* `-f=false` : одно тире для коротких имен, и значение булево (true/false)
* `--force` : двойное тире для длинных имен
* `-it` : группировка булевных опций, будет эквивалентно: -i -t

### Для типа строка, число, дата, длительность:


* `-e=value` : одно тире для коротких имен, через **равно** значение опции
* `-e value` : одно тире для коротких имен, через **пробел** значение опции
* `-Ivalue` : одно тире для коротких имен, и сразу значение опции
* `--extra=value` : двойное тире для длинных имен, через **равно** значение опции
* `--extra value` : двойное тире для длинных имен, через **пробел** значение опции

### Для массиво опций (МассивСтрок, МассивЧисел, МассивДат):
повторение опции создаст массив данных указанного типа опций:

* `-e PATH:/bin -e PATH:/usr/bin` : Массив содержащий `["/bin", "/usr/bin"]`
* `-ePATH:/bin -ePATH:/usr/bin` : Массив содержащий `["/bin", "/usr/bin"]`
* `-e=PATH:/bin -e=PATH:/usr/bin` : Массив содержащий `["/bin", "/usr/bin"]`
* `--env PATH:/bin --env PATH:/usr/bin` : Массив содержащий `["/bin", "/usr/bin"]`
* `--env=PATH:/bin --env=PATH:/usr/bin` : Массив содержащий `["/bin", "/usr/bin"]`

## Arguments

Аргументы могут быть следующих простых типов:
* Булево
* Строка
* Число
* Дата
* Длительность

Так и принимать массивы данных типов, на пример:

* МассивЧисел
* МассивСтрок
* МассивДат
* МассивДлительностей

Пример `Строки` аргумента:

```bsl
Отладка = Команда.Аргумент("PATH", "" ,"Описание аргумента")
    .ТСтрока() / тип опции Строка
    .ВОкружении("ИМЯ_ПЕРЕМЕННОЙ")
    .ПоУмолчанию(Ложь)
    .СкрытьВСправке(); // Любой тип
```
`ВОкружении` Возможна передача нескольких переменных окружения разделенных через **пробел**

Подробное описание возможностей параметров команд и приложения [](./docs/ПараметрКоманды.md)

## Операторы

Оператор `--` устанавливает метку завершению любых опций.
Все что следует за данным оператором, будет считаться аргументом, даже если начинается с **тире**


Для примера, если команда "ХочуФайл" принимает в качестве аргумента имя файла, но начинающегося с `-`, тогда строка запуска данной программы будет выглядеть так:

```bsl
ИмяФайла = Команда.Аргумент("FILE", "", "имя файла для создания")
```

Если нужное нам имя файла равно `-f` и выполнить нашу команду:

```
ХочуФайл -f
```

Будет выдана ошибка, т.к. `-f` распознано как опция, а не аргумент.
Для того, чтобы решить данную проблему, необходимо объявить окончание опций, через оператора `--`

```
ХочуФайл -- -f
```

Тогда определение аргументов будет работать корректно.

## Команды и подкоманды

1cli поддерживает создание команд и подкоманд.
Неограниченное количество вложенных подкоманд.
А так же установки синонимов для команд и подкоманд.

```bsl
Приложение = Новый КонсольноеПриложение("testapp", "Выполняет полезную работу");
КомандаAve = Приложение.ДобавитьПодкоманду("a ave", "Команда ave", КлассРеализацииПодкоманды);
```

* Первый аргумент, наименование команды, которое необходимо будет вводить для запуска
* Второй аргумент, описание команды, которое будет выводиться в справке
* Третий аргумент, КлассРеализацииКоманды

1cli поддерживает указание псевдонимов команд. Для примера:

```bsl
КомандаAve = Приложение.ДобавитьПодкоманду("start run r", "Команда start", КлассРеализацииПодкоманды);
```

Псевдонимы для команды будут `start`, `run`, и`r` можно использовать любой из них.

1cli поддерживает автоматическую инициализацию параметров команд.
Переданный класс должен реализовывать процедуру:

```bsl
Процедура ОписаниеКоманды(Команда) Экспорт

КонецПроцедуры
```

## Строка использования приложения (спек)

Синтаксис спек базируется на POSIX.

### Опции

Для определения опций можно использовать длинные и короткие имена опций:
```bsl
Команда.Спек = "-f";
```
И/или:
```bsl
Команда.Спек = "--force";
```
Пример добавления аргументов в команду:

```bsl
Команд.Опция("f force", ...);
```

### Аргументы

Правила наименования аргументов, имя должно содержать только символы в верхнем регистре:

Пример, использования аргументов в определении строки использования приложения
```bsl
Команда.Спек="SRC DST"
```

Пример добавления аргументов в команду:

```go
Команда.Аргумент("SRC", ...);
Команда.Аргумент("DST", ...);
```

### Сортировка строки использования

1cli позволяет произвольно настраивать порядок использования опций и аргументов:

```bsl
Команда.Спек = "-f -g NAME -h PATH";
```

## Необзательность

Для того, чтобы сделать аргументы или опции необязательными, их необходимо заключить в `[...]`:
```bsl
Команда.Спек = "[-x]";
```

### Выбор между

Для отражения выборка между несколькими опциями или аргументами используется символ `|`:

```bsl
Команда.Спек = "--server | --agent";
Команда.Спек = "-H | -L | -P";
Команда.Спек = "-f | PATH";
```

### Повторитель

Для повторения аргументов или опций необходимо использовать оператор `...`:

```bsl
Команда.Спек = "PATH...";
Команда.Спек = "-f...";
```

### Логические группы

Возможна логическая группировка опций и аргументов. Данную группировки стоит использовать и комбинировать с такими символами как `|` и `...`:

```bsl
Команда.Спек = "(-e КОМАНДА)... | (-x|-y)";
```
Данный пример, настраивает строку использования следующим образом:
* Повторение опции -e  и команды
* Или
* Выбор между -x  и -y

### Группировка опций

Все короткие опции (типа булево), возможно группировать в любом порядке вместе:
```bsl
Команда.Спек = "-abcd";
```
### Все опции

Для определение любой опции приложения:

```bsl
Команда.Спек = "[OPTIONS]";
```
Для примера, для команды с опциями a, b, c и d, указание `[OPTIONS]` будет аналогично указанию:

```bsl
Команда.Спек = "[-a | -b | -c | -d]...";
```

### Аннотация к опциям в строке использования

Можно задавать в строке использования `=<очень хороший текст>` аннотации после указания опции в длинной или короткой форме, для того чтобы определить дополнительное описание или значение по умолчанию.

Для примера:

```bsl
Команда.Спек = "[ -a=<абсолютный путь к файлу> | --timeout=<в секундах> ] ARG";
```
Данные аннотации игнорируются парсером, и не влияют на работу приложения

### Операторы

Оператор `--` устанавливает метку завершению любых опций.
Все что следует за данным оператором считается аргументами.

Более, сложный пример:

```bsl
Приложение.Спек = "work -lp [-- CMD [ARG...]]";
```

## Грамматика строки использования 

Используется упрощенная (EBNF grammar) грамматика при создании строки использования:

Описание символа         | Символ и использование
-------------------------|-------------------------------
Короткая опция           | -> '-' [A-Za-z]
Длинная опция            | -> '--' [A-Za-z][A-Za-z0-9]*
Соединенные опции        | -> '-' [A-Za-z]+
Все опции                | -> '[OPTIONS]'
Логическая группа        | -> '(' любые другие опции ')'
Необязательная           | -> '[' любые другие опции ']'
Повтор аргумента         | -> '...'
Конец повтора аргументов | -> '--'

Можно комбинировать в указанные символы как хочется, для того чтобы добиться любых необходимых проверок опций и аргументов.


## Строка использования по умолчанию

По умолчанию, если не установлена разработчиком иная, 1cli автоматически создает для приложения и каждой команды строки использования используя следующую логику:

* Начало с пустой строки
* Если определена хоть одна опция, добавляется `[OPTIONS]` к текущей строке использования
* Для каждого добавленного аргумента, добавляет его представление согласно очереди, обявления аргументов.

Для примера, при добавлении в команду следующих опций и аргументов :

```bsl

ИнициализацияГит = Команда.Опция("g git-init", Ложь, "инициализировать создание git репозитория").Флаговый();
ИнтерактивныйРежим = Команда.Опция("interactive", Ложь, "интерактивный режим ввода информации о приложении").Флаговый();
ИспользоватьПодкоманды = Команда.Опция("s support-subcommads", Ложь, "шаблон с поддержкой подкоманд").Флаговый();
ФайлКонфига = Команда.Опция("c config-file", "", "файл настроек генерации приложения");

НаименованиеПриложения = Команда.Аргумент("NAME", "", "наименование приложения");
ПутьКПапкеГенерации = Команда.Аргумент("PATH", "", "Путь к папке для генерации нового приложения");

```

Будет автоматически создана следующая строка использования данной команды:

```bsl
[OPTIONS] NAME PATH
```

## Доработка

Доработка проводиться по git-flow. Жду ваших PR.

## Лицензия

Смотри файл `LICENSE`.
